import java.time.LocalDate

// Top-level build file where you can add configuration options common to all subprojects/modules.
plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.kotlin.android) apply false
    alias(libs.plugins.kotlin.compose) apply false
    id("com.google.devtools.ksp") version "2.2.21-2.0.4" apply false
    kotlin("jvm")
}

fun executeGitCommand(command: String, default: String = ""): String {
    return try {
        providers.exec {
            commandLine(*command.split("\\s".toRegex()).toTypedArray())
        }.standardOutput.asText.get().trim()
    } catch (e: Exception) {
        e.printStackTrace()
        default
    }
}

val versionCode = executeGitCommand("git rev-list --count HEAD", "1")
val tagName = executeGitCommand("git tag --points-at HEAD")
val hash = executeGitCommand("git rev-parse --short HEAD", "nohash")
val branch = executeGitCommand("git rev-parse --abbrev-ref HEAD")
val outputsDir = "build/appOutputs/$branch/${tagName.ifBlank { "nightly/r$versionCode.$hash" }}"
val mdFileName = "ChangeLog.md"

ext {
    set("versionCode", versionCode)
    set("tagName", tagName)
    set("hash", hash)
    set("branch", branch)
    set("outputsDir", outputsDir)
    set("changeLog", mdFileName)
}


val changelogDir: File = project.rootProject.file(outputsDir)

tasks.register("generateChangelog") {
    group = "documentation"
    description = "ç”ŸæˆMDæ–‡ä»¶å¹¶åœ¨index.jsonä¸­å¼•ç”¨"

    doLast {
        val dateStr = LocalDate.now().toString()


        if (tagName.isBlank()) {
            println("å½“å‰ HEAD æ²¡æœ‰ Tagï¼Œè·³è¿‡ã€‚")
            return@doLast
        }

        val prevTag = executeGitCommand("git describe --tags --abbrev=0 HEAD^")
        val range = if (prevTag.isNotEmpty()) "$prevTag..HEAD" else "HEAD"

        println("å¤„ç†ç‰ˆæœ¬: $tagName (From $prevTag)")

        // è·å–å¹¶è§£æ Log
        val logs = executeGitCommand("git log $range --pretty=format:%s").lines().distinct()
        // åˆ†ç±»å®¹å™¨: Type -> List of "Scope: Message"
        val changes = mutableMapOf<String, MutableList<String>>()

        // æ­£åˆ™: type(scope): message
        val regex = "^(feat|fix|perf|refactor|docs|style|test|chore)(\\((.*)\\))?: (.*)$".toRegex()

        logs.forEach { line ->
            val match = regex.find(line)
            if (match != null) {
                val (type, _, scope, message) = match.destructured
                if (type in arrayOf("test", "chore", "style", "docs")) return@forEach
                // æ ¼å¼åŒ–å•è¡Œæ–‡æ¡ˆ: "Scope: Message" æˆ– "Message"
                val formattedMsg = if (scope.isNotBlank()) "**$scope**: $message" else message
                changes.getOrPut(type) { mutableListOf() }.add(formattedMsg)
            } else if (line.isNotBlank()) {
                changes.getOrPut("misc") { mutableListOf() }.add(line)
            }
        }

        if (changes.isEmpty()) {
            println("æ— å˜æ›´è®°å½•")
            return@doLast
        }

        val mdFile = File(changelogDir, mdFileName)

        val mdContent = StringBuilder()
        mdContent.append("# Release $tagName\n\n-----\n\n")
        mdContent.append("> å‘å¸ƒæ—¥æœŸ: $dateStr\n\n")

        // å®šä¹‰ç±»å‹æ˜¾ç¤ºçš„ä¼˜å…ˆçº§å’Œæ ‡é¢˜æ˜ å°„
        val typeHeaders = mapOf(
            "feat" to "âœ¨ New Features",
            "fix" to "ğŸ› Bug Fixes",
            "perf" to "âš¡ Performance",
            "refactor" to "â™»ï¸ Refactor",
            "misc" to "ğŸ”§ Others",
        )

        // æŒ‰ä¼˜å…ˆçº§é¡ºåºå†™å…¥
        typeHeaders.forEach { (type, header) ->
            changes[type]?.let { msgs ->
                mdContent.append("## $header\n\n")
                msgs.forEach { msg -> mdContent.append("- $msg\n") }
                mdContent.append("\n")
            }
        }

        // å†™å…¥ MD æ–‡ä»¶
        if (!(changelogDir.exists())) changelogDir.mkdirs()
        mdFile.writeText(mdContent.toString())
        println("Markdown ç”Ÿæˆå®Œæ¯•: ${mdFile.name}")
    }
}

tasks.named("assemble") {
    finalizedBy("generateChangelog")
}